property set MAP_Error_Properties is
with EMV2, MAP_Errors;

-- ====================================== --
-- Types related to the report background --
-- ====================================== --

Context : type aadlstring;

Assumption : type aadlstring;

Abbreviation : type record (
	Full : aadlstring;
	Definition : aadlstring;
);

-- =================================== --
-- Types related to STPA preliminaries --
-- =================================== --

Accident_Level : type record (
	Level : aadlinteger;
	Description: aadlstring;
);

Accident : type record (
	Description : aadlstring;
	Level : MAP_Error_Properties::Accident_Level; 
);

Hazard: type record (
	Description : aadlstring;
	Accident : MAP_Error_Properties::Accident;
);

Constraint: type record (
	Description : aadlstring;
	Hazard : MAP_Error_Properties::Hazard;
);

-- ============================ --
-- Types related to STPA step 1 --
-- ============================ --

KindsType : type enumeration (
	-- From Tbl 8.4 in Engineering a Safer World
	NotProviding, Providing, Early, Late, AppliedTooLong, StoppedTooSoon, 
	
	-- [Where are these from?]
	ValueLow, ValueHigh, 
	
	-- [Paper for Complex Parameters]
	ParamsMissing, ParamsWrong, ParamsOutOfOrder,
	
	-- These two values are for marking a control action as having been 
	-- considered, so you don't just have an empty table cell.
	NotHazardous, NotApplicable
);

Occurrence : record (
	Kind : MAP_Error_Properties::KindsType;
	ViolatedConstraint : MAP_Error_Properties::Constraint;
	Title : aadlstring;
	Cause : record (
		ErrorType : reference ({emv2}**error type);
		Description : aadlstring;
	);
	Compensation : aadlstring;	
) applies to (port connection, {emv2}**connection error source);

end MAP_Error_Properties;