package PCA_Shutoff_Logic
public
with PCA_Shutoff_Types, PCA_Shutoff_Properties, MAP_Properties, MAP_Error_Properties;

	process ICEpcaShutoffProcess
	features
		SpO2 : in event data port PCA_Shutoff_Types::SpO2;
		ETCO2 : in data port PCA_Shutoff_Types::ETCO2;
		RR : in event data port PCA_Shutoff_Types::RR;
		
		CapnographError : in event port;
		PulseOxError : in event port;
		
		CmdPumpNorm : out event data port PCA_Shutoff_Types::PumpCmd;
	flows
		spo2_flow: flow path SpO2 -> CmdPumpNorm;
	properties
		MAP_Properties::Process_Type => logic;
		MAP_Properties::Component_Type => controller;
		MAP_Error_Properties::ProcessModel => [
			name => "PatientStatus";
			values => ("Healthy", "AtRisk", "Overdosed");
		];
	annex EMV2 {** 
		use types PCA_Shutoff_Errors;
		use behavior PCA_Shutoff_Errors::AppStatus;
		error propagations
			SpO2 : in propagation {SpO2ValueHigh};
			ETCO2 : in propagation {ETCO2ValueHigh};
			RR : in propagation {RespiratoryRateLow, RespiratoryRateHigh};
			CmdPumpNorm : out propagation {InadvertentPumpNormally};
			flows
				HighSpO2LeadsToOD : error path SpO2{SpO2ValueHigh} -> CmdPumpNorm{InadvertentPumpNormally};
				HighETCO2LeadsToOD : error path ETCO2{ETCO2ValueHigh} -> CmdPumpNorm{InadvertentPumpNormally};
				LowRRLeadsToOD : error path RR{RespiratoryRateLow, RespiratoryRateHigh} -> CmdPumpNorm{InadvertentPumpNormally};
		end propagations;
		component error behavior
			events
				BadSpO2: error event {SpO2ValueHigh};
				BadETCO2 : error event {ETCO2ValueHigh};
				BadRR : error event {RespiratoryRateHigh};
				Dubs : error event {SpO2ValueHigh, ETCO2ValueHigh};
				AllBad : error event {SpO2ValueHigh, ETCO2ValueHigh, RespiratoryRateHigh};
			transitions
				SingleFailure : Normal -[ BadSpO2 or BadETCO2 or BadRR ]-> DetectedError;
				DoubleFailure : Normal -[ Dubs ]-> DetectedError;
				SimultaneousFailure : Normal -[ AllBad ]-> UndetectedError;
		end component;
		properties
			MAP_Error_Properties::Manifestation => ContentHigh applies to SpO2;
	
				
--			MAP_Error_Properties::ProtoHazard => [
--				Harm => PCA_Shutoff_Error_Properties::PatientDeath;
--				ComponentState => reference(PumpNormal);
--				EnvironmentState => reference(Risk);
--				InteractionPoints => (reference(SpO2), reference(ETCO2)); 
--			];
	**};
	end ICEpcaShutoffProcess;

	process implementation ICEpcaShutoffProcess.imp
	subcomponents
		UpdateSpO2Thread : thread UpdateSpO2Thread.imp;
		UpdateRRThread : thread UpdateRRThread.imp;
		PumpControlThread : thread PumpControlThread.imp;
		
		CapnographErrorThread : thread CapnographErrorThread.imp;
		PulseOxErrorThread : thread PulseOxErrorThread.imp;
	connections
		incoming_spo2 : port SpO2 -> UpdateSpO2Thread.SpO2In;
		incoming_rr : port RR -> UpdateRRThread.RRIn;
		--rr_to_pump_ctrl : port UpdateRRThread.RROut -> PumpControlThread.RR;
		--spo2_to_pump_ctrl : port UpdateSpO2Thread.SpO2Out -> PumpControlThread.SpO2;
		outgoing_pump_command : port PumpControlThread.PumpNorm -> CmdPumpNorm;
	
		incoming_capnograph_error : port CapnographError -> CapnographErrorThread.CapnographError;
		incoming_pulseox_error : port PulseOxError -> PulseOxErrorThread.PulseOxError;
	annex EMV2 {**
		use types PCA_Shutoff_Errors;
--		properties
--			MAP_Error_Properties::Occurrence => [
--				Kind => Providing; 
--				ViolatedConstraint => PCA_Shutoff_Error_Properties::DontLetPumpRunWhenUnsafe;
--				Title => "High Physio Params";
--				Cause => [
--					ErrorType => reference(InadvertentPumpNormally);
--					Description => "One or more physiological parameters are too high, leading the app logic to incorrectly believe the patient is healthy";
--				];
--				Compensation => "Physiological values are cross-checked with others";
--			] applies to outgoing_pump_command;
	**};

	end ICEpcaShutoffProcess.imp;
	
	thread UpdateSpO2Thread
	features
		SpO2In : in event data port PCA_Shutoff_Types::SpO2;
		SpO2Out : out event data port PCA_Shutoff_Types::SpO2;
	end UpdateSpO2Thread;
	
	thread implementation UpdateSpO2Thread.imp
	end UpdateSpO2Thread.imp;
	
	thread UpdateRRThread
	features
		RRIn : in event data port PCA_Shutoff_Types::RR;
		RROut : out event data port PCA_Shutoff_Types::RR;
	end UpdateRRThread;

	thread implementation UpdateRRThread.imp
	end UpdateRRThread.imp;
	
	thread PumpControlThread
	features
		RR : in event data port PCA_Shutoff_Types::RR;
		SpO2 : in event data port PCA_Shutoff_Types::SpO2;
		PumpNorm : out event data port PCA_Shutoff_Types::PumpCmd;
	properties
		Thread_Properties::Dispatch_Protocol => Periodic;
	end PumpControlThread;

	thread implementation PumpControlThread.imp
	end PumpControlThread.imp;

	thread CapnographErrorThread
	features
		CapnographError : in event port;
	end CapnographErrorThread;
	
	thread implementation CapnographErrorThread.imp
	end CapnographErrorThread.imp;
	
	thread PulseOxErrorThread
	features
		PulseOxError : in event port;
	end PulseOxErrorThread;

	thread implementation PulseOxErrorThread.imp
	end PulseOxErrorThread.imp;
	
end PCA_Shutoff_Logic;