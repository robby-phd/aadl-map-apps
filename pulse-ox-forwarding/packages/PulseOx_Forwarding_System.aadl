package PulseOx_Forwarding_System
public
with PulseOx_Interface, PulseOx_Forwarding_Logic, PulseOx_Forwarding_Display, 
	PulseOx_Forwarding_Properties, MAP_Properties, MAP_Error_Properties,
	PulseOx_Forwarding_Errors;

	system PulseOx_Forwarding_System
	end PulseOx_Forwarding_System;
	
	system implementation PulseOx_Forwarding_System.imp
	subcomponents
		-- Physiological inputs
		pulseOx : device PulseOx_Interface::ICEpoInterface.imp;
		
		-- App logic
		appLogic : process PulseOx_Forwarding_Logic::PulseOx_Logic_Process.imp;
		appDisplay : process PulseOx_Forwarding_Display::PulseOx_Display_Process.imp;
	connections
		-- From device to logic
		spo2_to_logic : port pulseOx.SpO2 -> appLogic.SpO2;
		
		-- From device to display
		spo2_to_display : port pulseOx.SpO2 -> appDisplay.SpO2
		{MAP_Properties::Channel_Delay => 150 ms;};
		
		-- From logic to display
		alarm_to_display : port appLogic.DerivedAlarm -> appDisplay.DerivedAlarm;
	annex EMV2 {**
		use types PulseOx_Forwarding_Errors;
		properties
		MAP_Error_Properties::Occurrence => [
			Kind => NotProviding;
			Hazard => PulseOx_Forwarding_Error_Properties::MissedAlarm;
			ViolatedConstraint => PulseOx_Forwarding_Error_Properties::ShowAllAlarms;
			Title => "Bad SpO2";
			Cause => "The SpO2 values from the pulse oximeter are too high, so the alarm is missed";
			Compensation => "None";
			Impact => reference(SpO2ValueHigh);
		] applies to alarm_to_display;
		MAP_Error_Properties::Occurrence => [
			Kind => ValueHigh;
			Hazard => PulseOx_Forwarding_Error_Properties::BadInfoDisplayed;
			ViolatedConstraint => PulseOx_Forwarding_Error_Properties::ShowGoodInfo;
			Title => "Bad SpO2";
			Cause => "Incorrect SpO2 values are sent to the display";
			Compensation => "None";
			Impact => reference(SpO2ValueHigh);
		] applies to spo2_to_display;
	**};
	end PulseOx_Forwarding_System.imp;
end PulseOx_Forwarding_System;